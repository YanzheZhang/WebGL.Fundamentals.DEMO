<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
    <title>WebGL 三维聚光灯-04</title>
    <link href="./03SpotLighting-01.css" rel="stylesheet" />
</head>
<body>
    <canvas id="canvas"></canvas>
    <div id="uiContainer">
        <div id="ui">
            <div id="fRotation"></div>
            <div id="lightRotationX"></div>
            <div id="lightRotationY"></div>
            <div id="innerLimit"></div>
            <div id="outerLimit"></div>
        </div>
    </div>
    <!-- vertex shader -->
    <script id="3d-vertex-shader" type="x-shader/x-vertex">
        attribute vec4 a_position;
        attribute vec3 a_normal;

        uniform vec3 u_lightWorldPosition;
        uniform vec3 u_viewWorldPosition;

        uniform mat4 u_world;
        uniform mat4 u_worldViewProjection;
        uniform mat4 u_worldInverseTranspose;

        varying vec3 v_normal;

        varying vec3 v_surfaceToLight;
        varying vec3 v_surfaceToView;

        void main() {
        // Multiply the position by the matrix.
        gl_Position = u_worldViewProjection * a_position;

        // orient the normals and pass to the fragment shader
        v_normal = mat3(u_worldInverseTranspose) * a_normal;

        // compute the world position of the surfoace
        vec3 surfaceWorldPosition = (u_world * a_position).xyz;

        // compute the vector of the surface to the light
        // and pass it to the fragment shader
        v_surfaceToLight = u_lightWorldPosition - surfaceWorldPosition;

        // compute the vector of the surface to the view/camera
        // and pass it to the fragment shader
        v_surfaceToView = u_viewWorldPosition - surfaceWorldPosition;
        }
    </script>
    <!-- fragment shader -->
    <script id="3d-fragment-shader" type="x-shader/x-fragment">
        precision mediump float;

        // Passed in from the vertex shader.
        varying vec3 v_normal;
        varying vec3 v_surfaceToLight;
        varying vec3 v_surfaceToView;

        uniform vec4 u_color;
        uniform float u_shininess;
        uniform vec3 u_lightDirection;
        uniform float u_innerLimit;          // in dot space
        uniform float u_outerLimit;          // in dot space

        void main() {
        // because v_normal is a varying it's interpolated
        // so it will not be a unit vector. Normalizing it
        // will make it a unit vector again
        vec3 normal = normalize(v_normal);

        vec3 surfaceToLightDirection = normalize(v_surfaceToLight);
        vec3 surfaceToViewDirection = normalize(v_surfaceToView);
        vec3 halfVector = normalize(surfaceToLightDirection + surfaceToViewDirection);

        float dotFromDirection = dot(surfaceToLightDirection,
        -u_lightDirection);
        float inLight = smoothstep(u_outerLimit, u_innerLimit, dotFromDirection);
        float light = inLight * dot(normal, surfaceToLightDirection);
        float specular = inLight * pow(dot(normal, halfVector), u_shininess);

        gl_FragColor = u_color;

        // Lets multiply just the color portion (not the alpha)
        // by the light
        gl_FragColor.rgb *= light;

        // Just add in the specular
        gl_FragColor.rgb += specular;
        }
    </script>
    <!--
    for most samples webgl-utils only provides shader compiling/linking and
    canvas resizing because why clutter the examples with code that's the same in every sample.
    See http://webglfundamentals.org/webgl/lessons/webgl-boilerplate.html
    and http://webglfundamentals.org/webgl/lessons/webgl-resizing-the-canvas.html
    for webgl-utils, m3, m4, and webgl-lessons-ui.
    -->
    <!--<script src="https://webglfundamentals.org/webgl/resources/webgl-utils.js"></script>
    <script src="https://webglfundamentals.org/webgl/resources/webgl-lessons-ui.js"></script>
    <script src="https://webglfundamentals.org/webgl/resources/m4.js"></script>-->
    <script src="../lib/webgl-lessons-ui.js"></script>
    <script src="../lib/webgl-utils.js"></script>
    <script src="../lib/m4.js"></script>
    <script src="./03SpotLighting-04.js"></script>
</body>
</html>
